// IMPORTANT: when adding new special type,
// don't forget to threat it as special in TypeDeclaration and hir_to_ir.rs
// IMPORTANT: add mapping for new functions with @mangle_as
// IMPORTANT: add precedence for new binary operators

//=================================
//              None
//=================================
type None

/// Convert `None` to `String`
fn <:None> as String => "none"
//---------------------------------


//=================================
//              Bool
//=================================
type Bool

/// Negate boolean value
fn not <x:Bool> -> Bool:
	if x:
		return false
	return true

/// Logic-and function
fn <x: Bool> and <y: Bool> -> Bool:
	if x:
		return y
	return false

/// Logic-or function
fn <x: Bool> or <y: Bool> -> Bool:
	if x:
		return true
	return y

/// Convert `Bool` to `String`
fn <x: Bool> as String -> String:
	if x:
		return "true"
	return "false"
//---------------------------------


//=================================
//            Integer
//=================================
type Integer

@mangle_as("integer_eq_integer")
fn <:Integer> == <:Integer> -> Bool

fn <x: Integer> != <y: Integer> => not (x == y)

@mangle_as("integer_less_integer")
fn <:Integer> < <:Integer> -> Bool

fn <x: Integer> > <y: Integer> => y < x

fn <x: Integer> <= <y: Integer> => not (x > y)

fn <x: Integer> >= <y: Integer> => not (x < y)

fn + <x: Integer> => x

@mangle_as("minus_integer")
fn - <:Integer> -> Integer

@mangle_as("integer_plus_integer")
fn <:Integer> + <:Integer> -> Integer

fn <x: Integer> - <y: Integer> => x + -y

@mangle_as("integer_star_integer")
fn <:Integer> * <:Integer> -> Integer

/// Convert `Integer` to `String`
@mangle_as("integer_as_string")
fn <:Integer> as String -> String
//---------------------------------

//=================================
//            Rational
//=================================
type Rational
//---------------------------------

//=================================
//            String
//=================================
type String

/// Prints string to stdout with newline
@mangle_as("print_string")
fn print <:String>

/// Trait for things that may be interpreted as string
trait AsString:
	fn <:Self> as String -> String

/// Print anything that may be interpreted as String
fn print <x: AsString> => print (x as String)
//---------------------------------