module = { SOI ~ annotated_statement* ~ EOI }

annotated_statement =  { annotation* ~ statement }
annotation          = ${ "@" ~ identifier ~ EOL }

statement = _{ declaration | expression_statement }

expression_statement = ${ expression ~ EOL }
expression           = _{ literal | type_reference }
literal              =  { NONE | boolean | rational | integer | string }
NONE                 =  { "none" }
boolean              = _{ TRUE | FALSE }
TRUE                 =  { "true" }
FALSE                =  { "false" }
rational             = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
integer              = @{ ASCII_DIGIT+ }
string               = @{ "\"" ~ (!("\"" | NEWLINE) ~ ANY)* ~ "\"" }
type_reference       =  { typename }

declaration = _{ function | type }

function            = ${ "fn" ~ WHITESPACE ~ function_name_parts ~ EOL }
function_name_parts = _{ function_name_part ~ (WHITESPACE ~ function_name_part)* }
function_name_part  = _{ text | parameter }
parameter           = !{ "<" ~ identifier ~ ":" ~ typename ~ ">" }

type     = ${ "type" ~ WHITESPACE ~ typename ~ EOL }
typename = @{ UPPERCASE_LETTER ~ (LETTER | NUMBER)* }

text       = @{ LETTER+ }
identifier = @{ LOWERCASE_LETTER ~ (LETTER | NUMBER)* }

EOL        = _{ NEWLINE | EOI }
WHITESPACE = _{ WHITE_SPACE }
