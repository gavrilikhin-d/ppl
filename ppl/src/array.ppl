use core.*

use memory.*

/// Array of values
type Array<T>:
	size: Integer
	capacity: Integer
	data: MemoryAddress

/// Create an empty array
fn<T> <:Type<T>>[] -> Array<T>:
	let capacity = 8
	let data = allocate capacity T
	return Array<T> { size: 0, capacity, data }

// TODO: errors, if out of borders
/// Get i-th element of an array
fn<T> <array: Reference<Array<T>>> [ <i: Integer> ] -> Reference<T>:
	let address = array.data + i * (size of T)
	return T at address

// TODO: errors, if out of borders
/// Get i-th element of an array
fn<T> <array: ReferenceMut<Array<T>>> [ <i: Integer> ] -> ReferenceMut<T>:
	let address = array.data + i * (size of T)
	return T at address

fn<T> push <x: T> to <array: ReferenceMut<Array<T>>>:
	if array.size == array.capacity:
		let new_capacity = array.capacity * 2
		let new_data = allocate new_capacity T
		let mut i = 0
		while i < array.size:
			let value = T at (new_data + i)
			value = array[i]
			i = i + 1
		free array.data
		array.data = new_data
		array.capacity = new_capacity

	let value = T at (array.data + array.size)
	value = x
	array.size = array.size + 1

fn<T> <array: Reference<Array<T>>> is empty => array.size == 0
fn<T> <array: Reference<Array<T>>> is not empty => array.size > 0